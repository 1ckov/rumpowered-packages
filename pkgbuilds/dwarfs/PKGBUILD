
pkgname=dwarfs
pkgver=0.6.1
pkgrel=2
pkgdesc="A fast high compression read-only file system"
url='https://github.com/mhx/dwarfs'
arch=('x86_64' 'aarch64')
license=('GPL3')
replaces=(
    'dwarfs-bin'
)
depends=(
  'fuse3' 'openssl' 'boost-libs' 'jemalloc' 'xxhash'
  'lz4' 'xz' 'zstd' 'libarchive'
  'libunwind' 'google-glog' 'fmt' 'gflags' 'double-conversion'
)
makedepends=(
  'cmake' 'ruby-ronn'
  'boost' 'libevent' 'libdwarf'
)
source=(
    "$pkgname-$pkgver.tar.bz2::https://github.com/mhx/dwarfs/releases/download/v$pkgver/dwarfs-$pkgver.tar.bz2"
    max_queued_bytes.patch
    out_of_bounds.patch
    )
cksums=('3387403462'
        '1471711936'
        '2146247846')
sha256sums=('d6968197c6f7b30a946cf77bc94922de78c12e763da42b08c71fdfe87908dc5d'
            '1b9a5bbd0c54a6a1dd883cf9052ca2cd31b515d5cf3d3bca9f83721e7f327f42'
            '9e7232cb2ffafe4e332321a03acb3b12f4991937e4f97363605b2d994fd39a16')
b2sums=('8642a9b204b96f8c05933ded570ec7cbcfeb9f01adb97031251faa22cfe76bc1fe09cac4f1ec5496623dcdebeda2f23a79c7e946ca4371c0f58fe0a427d0a2e1'
        'dbb0d9fa6e731c17c7eb3462b6ca244374afe7c5c3ddf8e3b2bba036788f65c34baeb3fa568bdde2d638ead677c9a083f3b130b777adf0f6615eb1fc58a078b3'
        '78bf091485ef945f8f6bce6a9778abd3a02dceb289dcc07eae85bdf75012f33d602a166a940adeec17f7dd3012a330474ffc7846b9977a3f117b36a45e2be137')

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i "$srcdir/max_queued_bytes.patch"
  patch -p1 -i "$srcdir/out_of_bounds.patch"
}

build() {
  cmake -B build -S "$pkgname-$pkgver" \
    -D CMAKE_INSTALL_PREFIX=/usr
    
  make -C build
}

package() {
  make -j$(nproc) -C build DESTDIR="$pkgdir" install

  mv "$pkgdir/usr/sbin"/* "$pkgdir/usr/bin"
  rm -rf "$pkgdir/usr/sbin"

  install -Dm0644 "$pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
